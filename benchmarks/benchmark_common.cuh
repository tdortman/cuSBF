#pragma once

#include <benchmark/benchmark.h>
#include <cuda/__cmath/ceil_div.h>
#include <cuda_runtime.h>
#include <thrust/count.h>
#include <thrust/device_ptr.h>
#include <thrust/device_vector.h>
#include <thrust/execution_policy.h>
#include <thrust/iterator/counting_iterator.h>
#include <thrust/random.h>
#include <thrust/transform.h>

#include <algorithm>
#include <chrono>
#include <cstdint>
#include <cstdio>
#include <filesystem>
#include <fstream>
#include <memory>
#include <stdexcept>
#include <string>
#include <thread>
#include <unordered_map>
#include <vector>

#include <unistd.h>

#include <cuda/std/bit>

#include <cusbf/BloomFilter.cuh>
#include <cusbf/device_span.cuh>
#include <cusbf/helpers.cuh>
#include <cusbf/superbloom_ffi.hpp>

namespace benchmark_common {

inline bool g_cpuFastxParallelizeRecords = false;
inline uint64_t g_cpuFastxNumRecords =
    std::max<uint64_t>(1, static_cast<uint64_t>(std::thread::hardware_concurrency()));

inline uint64_t splitSequenceKmers(uint64_t sequenceLength, uint64_t numRecords, uint64_t k) {
    if (numRecords == 0) {
        return 0;
    }

    const uint64_t perRecordBases = sequenceLength / numRecords;
    uint64_t totalKmers = 0;
    uint64_t pos = 0;
    for (uint64_t r = 0; r < numRecords; ++r) {
        const uint64_t thisLen = (r == numRecords - 1) ? sequenceLength - pos : perRecordBases;
        totalKmers += thisLen >= k ? thisLen - k + 1 : 0;
        pos += thisLen;
    }
    return totalKmers;
}

inline std::string
writeGeneratedFasta(const std::vector<char>& sequence, uint64_t numRecords, const char* prefix) {
    if (numRecords == 0) {
        throw std::runtime_error("FASTA record count must be >= 1");
    }

    const auto tempDir = std::filesystem::temp_directory_path();
    const auto path = tempDir / (std::string(prefix) + "-" + std::to_string(getpid()) + "-" +
                                 std::to_string(numRecords) + ".fasta");

    std::ofstream out(path, std::ios::binary | std::ios::trunc);
    if (!out) {
        throw std::runtime_error("failed to create temporary FASTA file");
    }

    const uint64_t perRecordBases = sequence.size() / numRecords;
    uint64_t pos = 0;
    for (uint64_t r = 0; r < numRecords; ++r) {
        const uint64_t thisLen = (r == numRecords - 1) ? sequence.size() - pos : perRecordBases;
        out << ">record_" << r << '\n';
        out.write(sequence.data() + pos, static_cast<std::streamsize>(thisLen));
        out.put('\n');
        pos += thisLen;
    }

    if (!out) {
        throw std::runtime_error("failed while writing temporary FASTA file");
    }

    return path.string();
}

class GPUTimer {
   public:
    GPUTimer() {
        CUSBF_CUDA_CALL(cudaEventCreate(&start_));
        CUSBF_CUDA_CALL(cudaEventCreate(&stop_));
    }

    ~GPUTimer() {
        cudaEventDestroy(start_);
        cudaEventDestroy(stop_);
    }

    GPUTimer(const GPUTimer&) = delete;
    GPUTimer& operator=(const GPUTimer&) = delete;

    void start(cudaStream_t stream = {}) {
        CUSBF_CUDA_CALL(cudaEventRecord(start_, stream));
    }

    [[nodiscard]] double elapsed(cudaStream_t stream = {}) {
        CUSBF_CUDA_CALL(cudaEventRecord(stop_, stream));
        CUSBF_CUDA_CALL(cudaEventSynchronize(stop_));

        float milliseconds = 0.0f;
        CUSBF_CUDA_CALL(cudaEventElapsedTime(&milliseconds, start_, stop_));
        return static_cast<double>(milliseconds) / 1000.0;
    }

   private:
    cudaEvent_t start_{};
    cudaEvent_t stop_{};
};

class CPUTimer {
   public:
    void start() {
        start_ = std::chrono::high_resolution_clock::now();
    }

    [[nodiscard]] double elapsed() {
        auto end = std::chrono::high_resolution_clock::now();
        return std::chrono::duration<double>(end - start_).count();
    }

   private:
    std::chrono::time_point<std::chrono::high_resolution_clock> start_{};
};

// Concatenate all records in a FASTA/FASTQ file into a single sequence,
// inserting @p separator between records.
inline std::vector<char> readFastxConcatenated(std::string_view path, char separator = 'N') {
    auto input = cusbf::detail::openFastxFile(path);
    cusbf::detail::FastxReader reader(*input, path);
    cusbf::detail::FastxRecord record;

    std::vector<char> sequence;
    bool firstRecord = true;

    while (reader.nextRecord(record)) {
        if (!firstRecord) {
            sequence.push_back(separator);
        }
        firstRecord = false;
        sequence.insert(sequence.end(), record.sequence.begin(), record.sequence.end());
    }

    return sequence;
}

inline void setCommonCounters(
    benchmark::State& state,
    uint64_t memoryBytes,
    uint64_t itemsProcessed,
    uint64_t sequenceBases
) {
    state.SetItemsProcessed(static_cast<int64_t>(state.iterations() * itemsProcessed));
    state.counters["sequence_bases"] = benchmark::Counter(static_cast<double>(sequenceBases));
    state.counters["memory_bytes"] = benchmark::Counter(
        static_cast<double>(memoryBytes), benchmark::Counter::kDefaults, benchmark::Counter::kIs1024
    );
    state.counters["bits_per_item"] = benchmark::Counter(
        static_cast<double>(memoryBytes * 8) / static_cast<double>(itemsProcessed),
        benchmark::Counter::kDefaults,
        benchmark::Counter::kIs1024
    );
    state.counters["fpr_percentage"] = 0.0;
    state.counters["false_positives"] = 0.0;
}

inline void setFprCounters(benchmark::State& state, uint64_t falsePositives, uint64_t numKmers) {
    state.counters["false_positives"] = benchmark::Counter(static_cast<double>(falsePositives));
    state.counters["fpr_percentage"] = benchmark::Counter(
        100.0 * static_cast<double>(falsePositives) / static_cast<double>(numKmers)
    );
}

inline void setBenchmarkCounters(
    benchmark::State& state,
    uint64_t memoryBytes,
    uint64_t sequenceLength,
    uint64_t numKmers
) {
    setCommonCounters(state, memoryBytes, numKmers, sequenceLength);
    state.counters["num_kmers"] = benchmark::Counter(static_cast<double>(numKmers));
}

namespace detail {

__device__ __forceinline__ char randomDnaBase(uint64_t idx, uint32_t seed) {
    thrust::default_random_engine rng(seed);
    thrust::uniform_int_distribution<uint32_t> dist(0, 3);
    rng.discard(idx);
    constexpr char bases[] = {'A', 'C', 'G', 'T'};
    return bases[dist(rng)];
}

__device__ __forceinline__ char randomProteinSymbol(uint64_t idx, uint32_t seed) {
    thrust::default_random_engine rng(seed);
    thrust::uniform_int_distribution<uint32_t> dist(0, 19);
    rng.discard(idx);
    constexpr char symbols[] = {'A', 'C', 'D', 'E', 'F', 'G', 'H', 'I', 'K', 'L',
                                'M', 'N', 'P', 'Q', 'R', 'S', 'T', 'V', 'W', 'Y'};
    return symbols[dist(rng)];
}

}  // namespace detail

inline void
gpuGenerateDna(thrust::device_vector<char>& d_seq, uint64_t length, uint32_t seed = 42) {
    d_seq.resize(length);
    thrust::transform(
        thrust::counting_iterator<uint64_t>(0),
        thrust::counting_iterator<uint64_t>(length),
        d_seq.begin(),
        [seed] __device__(uint64_t idx) { return detail::randomDnaBase(idx, seed); }
    );
}

inline void
gpuGenerateProtein(thrust::device_vector<char>& d_seq, uint64_t length, uint32_t seed = 42) {
    d_seq.resize(length);
    thrust::transform(
        thrust::counting_iterator<uint64_t>(0),
        thrust::counting_iterator<uint64_t>(length),
        d_seq.begin(),
        [seed] __device__(uint64_t idx) { return detail::randomProteinSymbol(idx, seed); }
    );
}

inline void gpuGeneratePackedKmers(
    thrust::device_vector<uint64_t>& d_kmers,
    uint64_t count,
    uint32_t seed = 42
) {
    d_kmers.resize(count);
    thrust::transform(
        thrust::counting_iterator<uint64_t>(0),
        thrust::counting_iterator<uint64_t>(count),
        d_kmers.begin(),
        [seed] __device__(uint64_t idx) {
            thrust::default_random_engine rng(seed);
            thrust::uniform_int_distribution<uint64_t> dist(0, UINT64_MAX);
            rng.discard(idx);
            return dist(rng);
        }
    );
}

template <uint64_t K, typename Alphabet = cusbf::DnaAlphabet>
__global__ void encodePackedKmersKernel(const char* sequence, uint64_t numKmers, uint64_t* output) {
    constexpr uint64_t symbolBits = cuda::std::bit_width(Alphabet::symbolCount - 1);
    constexpr uint64_t symbolMask = (uint64_t{1} << symbolBits) - 1;
    const uint64_t idx = static_cast<uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    if (idx >= numKmers) {
        return;
    }

    uint64_t packed = 0;
    for (uint64_t i = 0; i < K; ++i) {
        const uint8_t encoded = Alphabet::encode(sequence + (idx + i) * Alphabet::symbolWidth);
        packed = (packed << symbolBits) | (encoded & symbolMask);
    }
    output[idx] = packed;
}

template <uint64_t K, typename Alphabet = cusbf::DnaAlphabet>
inline void gpuEncodePackedKmers(
    const char* d_sequence,
    uint64_t sequenceLength,
    uint64_t* d_output,
    cudaStream_t stream = {}
) {
    const uint64_t symbols = sequenceLength / Alphabet::symbolWidth;
    const uint64_t numKmers = symbols >= K ? symbols - K + 1 : 0;
    if (numKmers == 0) {
        return;
    }
    constexpr uint64_t blockSize = 256;
    const uint64_t gridSize = cuda::ceil_div(numKmers, blockSize);
    encodePackedKmersKernel<K, Alphabet>
        <<<gridSize, blockSize, 0, stream>>>(d_sequence, numKmers, d_output);
}

template <uint64_t K, typename Alphabet = cusbf::DnaAlphabet>
struct BenchmarkData {
    uint64_t sequenceLength{};
    uint64_t numKmers{};

    thrust::device_vector<char> d_throughputSequence;
    thrust::device_vector<uint64_t> d_throughputPackedKmers;

    thrust::device_vector<char> d_fprInsertSequence;
    thrust::device_vector<uint64_t> d_fprInsertPackedKmers;
    thrust::device_vector<char> d_zeroOverlapSequence;
    thrust::device_vector<uint64_t> d_zeroOverlapPackedKmers;
    bool fprDataReady = false;

    void generateThroughputData() {
        if constexpr (std::is_same_v<Alphabet, cusbf::ProteinAlphabet>) {
            gpuGenerateProtein(d_throughputSequence, sequenceLength, 42);
        } else {
            gpuGenerateDna(d_throughputSequence, sequenceLength, 42);
        }
        numKmers = sequenceLength >= K ? sequenceLength - K + 1 : 0;
        d_throughputPackedKmers.resize(numKmers);
    }

    void ensureFprData() const {
        if (fprDataReady) {
            return;
        }
        const_cast<BenchmarkData*>(this)->generateFprData();
    }

   private:
    void generateFprData() {
        if constexpr (std::is_same_v<Alphabet, cusbf::ProteinAlphabet>) {
            gpuGenerateProtein(d_fprInsertSequence, sequenceLength, 7);
            gpuGenerateProtein(d_zeroOverlapSequence, sequenceLength, 1337);
        } else {
            gpuGenerateDna(d_fprInsertSequence, sequenceLength, 7);
            gpuGenerateDna(d_zeroOverlapSequence, sequenceLength, 1337);
        }
        const uint64_t fprNumKmers = sequenceLength >= K ? sequenceLength - K + 1 : 0;
        d_fprInsertPackedKmers.resize(fprNumKmers);
        gpuEncodePackedKmers<K, Alphabet>(
            thrust::raw_pointer_cast(d_fprInsertSequence.data()),
            sequenceLength,
            thrust::raw_pointer_cast(d_fprInsertPackedKmers.data())
        );

        d_zeroOverlapPackedKmers.resize(fprNumKmers);
        gpuEncodePackedKmers<K, Alphabet>(
            thrust::raw_pointer_cast(d_zeroOverlapSequence.data()),
            sequenceLength,
            thrust::raw_pointer_cast(d_zeroOverlapPackedKmers.data())
        );

        CUSBF_CUDA_CALL(cudaDeviceSynchronize());

        fprDataReady = true;
    }
};

template <uint64_t K, typename Alphabet = cusbf::DnaAlphabet>
BenchmarkData<K, Alphabet>& getBenchmarkData(uint64_t length) {
    static std::unordered_map<uint64_t, BenchmarkData<K, Alphabet>> cache;

    auto it = cache.find(length);
    if (it != cache.end()) {
        return it->second;
    }

    cache.clear();

    BenchmarkData<K, Alphabet> data;
    data.sequenceLength = length;
    data.generateThroughputData();
    CUSBF_CUDA_CALL(cudaDeviceSynchronize());

    return cache.emplace(length, std::move(data)).first->second;
}

template <typename Config>
class CuSbfFixtureBase : public benchmark::Fixture {
   public:
    static constexpr uint64_t k = Config::k;

    void setupCommon(const benchmark::State& state) {
        sequenceLength = static_cast<uint64_t>(state.range(0));
        benchData = &getBenchmarkData<Config::k, typename Config::Alphabet>(sequenceLength);

        numKmers = benchData->numKmers;
        numSmers = sequenceLength - Config::s + 1;

        const uint64_t requestedFilterBits = cuda::std::bit_ceil(numKmers * 16);
        filter = std::make_unique<cusbf::Filter<Config>>(requestedFilterBits);
        filterMemory = filter->filterBits() / 8;
        d_output.resize(numKmers);
    }

    void tearDownCommon() {
        filter.reset();
        benchData = nullptr;
        d_output.clear();
        d_output.shrink_to_fit();
    }

    void setCounters(benchmark::State& state) const {
        setBenchmarkCounters(state, filterMemory, sequenceLength, numKmers);
        state.counters["s"] = benchmark::Counter(static_cast<double>(Config::s));
        state.counters["hashes"] = benchmark::Counter(static_cast<double>(Config::hashCount));
    }

    uint64_t sequenceLength{};
    uint64_t numKmers{};
    uint64_t numSmers{};
    uint64_t filterMemory{};
    BenchmarkData<Config::k, typename Config::Alphabet>* benchData{};
    thrust::device_vector<uint8_t> d_output;
    std::unique_ptr<cusbf::Filter<Config>> filter;
    GPUTimer timer;
};

template <typename Config>
class CuSbfConfigFixture : public CuSbfFixtureBase<Config> {
    using benchmark::Fixture::SetUp;
    using benchmark::Fixture::TearDown;

   public:
    void SetUp(const benchmark::State& state) override {
        this->setupCommon(state);
    }

    void TearDown(const benchmark::State&) override {
        this->tearDownCommon();
    }
};

template <typename Fixture>
void runCuSbfInsert(Fixture& fixture, benchmark::State& state) {
    for (auto _ : state) {
        fixture.filter->clear();
        CUSBF_CUDA_CALL(cudaDeviceSynchronize());

        fixture.timer.start();
        benchmark::DoNotOptimize(fixture.filter->insertSequenceDevice(
            cusbf::device_span<const char>{
                thrust::raw_pointer_cast(fixture.benchData->d_throughputSequence.data()),
                fixture.sequenceLength
            }
        ));
        const double elapsed = fixture.timer.elapsed();
        state.SetIterationTime(elapsed);
    }
    fixture.setCounters(state);
}

template <typename Fixture>
void runCuSbfQuery(Fixture& fixture, benchmark::State& state) {
    fixture.filter->clear();
    benchmark::DoNotOptimize(fixture.filter->insertSequenceDevice(
        cusbf::device_span<const char>{
            thrust::raw_pointer_cast(fixture.benchData->d_throughputSequence.data()),
            fixture.sequenceLength
        }
    ));
    CUSBF_CUDA_CALL(cudaDeviceSynchronize());

    for (auto _ : state) {
        fixture.timer.start();
        fixture.filter->containsSequenceDevice(
            cusbf::device_span<const char>{
                thrust::raw_pointer_cast(fixture.benchData->d_throughputSequence.data()),
                fixture.sequenceLength
            },
            cusbf::device_span<uint8_t>{
                thrust::raw_pointer_cast(fixture.d_output.data()), fixture.d_output.size()
            }
        );
        const double elapsed = fixture.timer.elapsed();
        state.SetIterationTime(elapsed);
        benchmark::DoNotOptimize(thrust::raw_pointer_cast(fixture.d_output.data()));
    }
    fixture.setCounters(state);
}

template <typename Fixture>
void runCuSbfFpr(Fixture& fixture, benchmark::State& state) {
    fixture.benchData->ensureFprData();

    fixture.filter->clear();
    benchmark::DoNotOptimize(fixture.filter->insertSequenceDevice(
        cusbf::device_span<const char>{
            thrust::raw_pointer_cast(fixture.benchData->d_fprInsertSequence.data()),
            fixture.sequenceLength
        }
    ));
    CUSBF_CUDA_CALL(cudaDeviceSynchronize());

    uint64_t falsePositives = 0;
    for (auto _ : state) {
        fixture.timer.start();
        fixture.filter->containsSequenceDevice(
            cusbf::device_span<const char>{
                thrust::raw_pointer_cast(fixture.benchData->d_zeroOverlapSequence.data()),
                fixture.sequenceLength
            },
            cusbf::device_span<uint8_t>{
                thrust::raw_pointer_cast(fixture.d_output.data()), fixture.d_output.size()
            }
        );
        const double elapsed = fixture.timer.elapsed();
        state.SetIterationTime(elapsed);
        benchmark::DoNotOptimize(thrust::raw_pointer_cast(fixture.d_output.data()));
    }

    falsePositives = static_cast<uint64_t>(
        thrust::count(fixture.d_output.begin(), fixture.d_output.end(), uint8_t{1})
    );
    fixture.setCounters(state);
    setFprCounters(state, falsePositives, fixture.numKmers);
}

// CPU SuperBloom fixture and runners

/// Compute the bit_vector_size_exponent and block_size_exponent for a CPU
/// SuperBloom filter that gives at least 16 bits per item while satisfying
/// the Rust library's SHARD_COUNT constraint (nb_blocks >= 1024).
inline void cpuFilterExponents(uint64_t numKmers, uint8_t& bitExp, uint8_t& blockExp) {
    constexpr uint64_t kBitsPerItem = 16;
    uint64_t filterBits = cuda::std::bit_ceil(numKmers * kBitsPerItem);
    constexpr uint64_t kMinFilterBits = uint64_t{1} << 19;
    filterBits = std::max(filterBits, kMinFilterBits);
    bitExp = static_cast<uint8_t>(cuda::std::bit_width(filterBits) - 1);
    blockExp = 9;
}

template <typename Config>
class SuperBloomCpuFixture : public benchmark::Fixture {
   public:
    using benchmark::Fixture::SetUp;
    using benchmark::Fixture::TearDown;

    static constexpr uint64_t k = Config::k;
    static constexpr uint64_t m = Config::m;
    static constexpr uint64_t s = Config::s;
    static constexpr uint64_t hashCount = Config::hashCount;
    using Alphabet = typename Config::Alphabet;
    using ConfigType = Config;

    void SetUp(const benchmark::State& state) override {
        initFailed_ = false;
        sequenceLength = static_cast<uint64_t>(state.range(0));
        benchData = &getBenchmarkData<Config::k, Alphabet>(sequenceLength);
        numKmers = g_cpuFastxParallelizeRecords
                       ? splitSequenceKmers(sequenceLength, g_cpuFastxNumRecords, Config::k)
                       : benchData->numKmers;

        cpuFilterExponents(numKmers, bitVectorSizeExp, blockSizeExp);

        unsigned n = std::thread::hardware_concurrency();
        threadCount_ = n > 0 ? static_cast<size_t>(n) : 0;

        recreateFilter();
        if (!handle_) {
            initFailed_ = true;
            return;
        }

        filterBits = uint64_t{1} << bitVectorSizeExp;
        h_output.resize(numKmers);
    }

    void TearDown(const benchmark::State&) override {
        if (handle_)
            superbloom_destroy(handle_);
        handle_ = nullptr;
        benchData = nullptr;
        h_output.clear();
        h_sequence.clear();
        if (!throughputFastxPath.empty()) {
            std::filesystem::remove(throughputFastxPath);
            throughputFastxPath.clear();
        }
        if (!fprInsertFastxPath.empty()) {
            std::filesystem::remove(fprInsertFastxPath);
            fprInsertFastxPath.clear();
        }
        if (!fprQueryFastxPath.empty()) {
            std::filesystem::remove(fprQueryFastxPath);
            fprQueryFastxPath.clear();
        }
    }

    void setCounters(benchmark::State& state) const {
        setBenchmarkCounters(state, filterBits / 8, sequenceLength, numKmers);
        state.counters["s"] = static_cast<double>(Config::s);
        state.counters["hashes"] = static_cast<double>(Config::hashCount);
    }

    void ensureHostSequence() {
        if (!h_sequence.empty() || sequenceLength == 0)
            return;
        h_sequence.resize(sequenceLength);
        CUSBF_CUDA_CALL(cudaMemcpy(
            h_sequence.data(),
            thrust::raw_pointer_cast(benchData->d_throughputSequence.data()),
            sequenceLength,
            cudaMemcpyDeviceToHost
        ));
    }

    void recreateFilter() {
        superbloom_destroy(handle_);
        handle_ = superbloom_create(
            Config::k, Config::m, Config::s, Config::hashCount, bitVectorSizeExp, blockSizeExp
        );
        if (handle_ && threadCount_ > 0) {
            superbloom_set_threads(handle_, threadCount_);
        }
    }

    void ensureThroughputFastxPath() {
        if (!g_cpuFastxParallelizeRecords || !throughputFastxPath.empty()) {
            return;
        }
        ensureHostSequence();
        throughputFastxPath = writeGeneratedFasta(
            h_sequence, g_cpuFastxNumRecords, "bloom-cpu-filter-comparison-throughput"
        );
    }

    void ensureFprFastxPaths() {
        if (!g_cpuFastxParallelizeRecords ||
            (!fprInsertFastxPath.empty() && !fprQueryFastxPath.empty())) {
            return;
        }

        benchData->ensureFprData();

        std::vector<char> fprHostSeq(sequenceLength);
        CUSBF_CUDA_CALL(cudaMemcpy(
            fprHostSeq.data(),
            thrust::raw_pointer_cast(benchData->d_fprInsertSequence.data()),
            sequenceLength,
            cudaMemcpyDeviceToHost
        ));

        std::vector<char> zeroHostSeq(sequenceLength);
        CUSBF_CUDA_CALL(cudaMemcpy(
            zeroHostSeq.data(),
            thrust::raw_pointer_cast(benchData->d_zeroOverlapSequence.data()),
            sequenceLength,
            cudaMemcpyDeviceToHost
        ));

        fprInsertFastxPath = writeGeneratedFasta(
            fprHostSeq, g_cpuFastxNumRecords, "bloom-cpu-filter-comparison-fpr-insert"
        );
        fprQueryFastxPath = writeGeneratedFasta(
            zeroHostSeq, g_cpuFastxNumRecords, "bloom-cpu-filter-comparison-fpr-query"
        );
    }

    uint64_t sequenceLength{};
    uint64_t numKmers{};
    uint64_t filterBits{};
    uint8_t bitVectorSizeExp{};
    uint8_t blockSizeExp{};
    BenchmarkData<Config::k, Alphabet>* benchData{};
    std::vector<uint8_t> h_output;
    std::vector<char> h_sequence;
    std::string throughputFastxPath;
    std::string fprInsertFastxPath;
    std::string fprQueryFastxPath;
    void* handle_{};
    bool initFailed_ = false;
    size_t threadCount_ = 0;
    CPUTimer timer;
};

template <typename Fixture>
void runSuperBloomCpuInsert(Fixture& fixture, benchmark::State& state) {
    if (g_cpuFastxParallelizeRecords) {
        fixture.ensureThroughputFastxPath();
    } else {
        fixture.ensureHostSequence();
    }

    for (auto _ : state) {
        fixture.recreateFilter();
        if (!fixture.handle_) {
            state.SkipWithError("superbloom_create failed during iteration");
            return;
        }

        fixture.timer.start();
        auto added =
            g_cpuFastxParallelizeRecords
                ? superbloom_insert_fastx_path(fixture.handle_, fixture.throughputFastxPath.c_str())
                : superbloom_insert_sequence(
                      fixture.handle_,
                      reinterpret_cast<const uint8_t*>(fixture.h_sequence.data()),
                      fixture.sequenceLength
                  );
        if (added < 0) {
            state.SkipWithError("superbloom insert failed");
            return;
        }
        state.SetIterationTime(fixture.timer.elapsed());
        benchmark::DoNotOptimize(added);
    }
    fixture.setCounters(state);
}

template <typename Fixture>
void runSuperBloomCpuQuery(Fixture& fixture, benchmark::State& state) {
    if (g_cpuFastxParallelizeRecords) {
        fixture.ensureThroughputFastxPath();
    } else {
        fixture.ensureHostSequence();
    }

    fixture.recreateFilter();
    if (!fixture.handle_) {
        state.SkipWithError("superbloom_create failed");
        return;
    }
    if (g_cpuFastxParallelizeRecords) {
        if (superbloom_insert_fastx_path(fixture.handle_, fixture.throughputFastxPath.c_str()) <
            0) {
            state.SkipWithError("superbloom insert failed");
            return;
        }
    } else {
        if (superbloom_insert_sequence(
                fixture.handle_,
                reinterpret_cast<const uint8_t*>(fixture.h_sequence.data()),
                fixture.sequenceLength
            ) < 0) {
            state.SkipWithError("superbloom insert failed");
            return;
        }
    }
    superbloom_freeze(fixture.handle_);

    for (auto _ : state) {
        fixture.timer.start();
        auto positives =
            g_cpuFastxParallelizeRecords
                ? superbloom_query_fastx_path(fixture.handle_, fixture.throughputFastxPath.c_str())
                : superbloom_query_sequence(
                      fixture.handle_,
                      reinterpret_cast<const uint8_t*>(fixture.h_sequence.data()),
                      fixture.sequenceLength
                  );
        if (positives < 0) {
            state.SkipWithError("superbloom query failed");
            return;
        }
        state.SetIterationTime(fixture.timer.elapsed());
        benchmark::DoNotOptimize(positives);
    }
    fixture.setCounters(state);
}

template <typename Fixture>
void runSuperBloomCpuFpr(Fixture& fixture, benchmark::State& state) {
    if (g_cpuFastxParallelizeRecords) {
        fixture.ensureFprFastxPaths();
    } else {
        fixture.benchData->ensureFprData();
    }

    std::vector<char> fprHostSeq(fixture.sequenceLength);
    if (!g_cpuFastxParallelizeRecords) {
        CUSBF_CUDA_CALL(cudaMemcpy(
            fprHostSeq.data(),
            thrust::raw_pointer_cast(fixture.benchData->d_fprInsertSequence.data()),
            fixture.sequenceLength,
            cudaMemcpyDeviceToHost
        ));
    }

    std::vector<char> zeroHostSeq(fixture.sequenceLength);
    if (!g_cpuFastxParallelizeRecords) {
        CUSBF_CUDA_CALL(cudaMemcpy(
            zeroHostSeq.data(),
            thrust::raw_pointer_cast(fixture.benchData->d_zeroOverlapSequence.data()),
            fixture.sequenceLength,
            cudaMemcpyDeviceToHost
        ));
    }

    fixture.recreateFilter();
    if (!fixture.handle_) {
        state.SkipWithError("superbloom_create failed");
        return;
    }
    if (g_cpuFastxParallelizeRecords) {
        if (superbloom_insert_fastx_path(fixture.handle_, fixture.fprInsertFastxPath.c_str()) < 0) {
            state.SkipWithError("superbloom insert failed");
            return;
        }
    } else {
        if (superbloom_insert_sequence(
                fixture.handle_,
                reinterpret_cast<const uint8_t*>(fprHostSeq.data()),
                fixture.sequenceLength
            ) < 0) {
            state.SkipWithError("superbloom insert failed");
            return;
        }
    }
    superbloom_freeze(fixture.handle_);

    uint64_t falsePositives = 0;
    for (auto _ : state) {
        fixture.timer.start();
        auto positives =
            g_cpuFastxParallelizeRecords
                ? superbloom_query_fastx_path(fixture.handle_, fixture.fprQueryFastxPath.c_str())
                : superbloom_query_sequence(
                      fixture.handle_,
                      reinterpret_cast<const uint8_t*>(zeroHostSeq.data()),
                      fixture.sequenceLength
                  );
        if (positives < 0) {
            state.SkipWithError("superbloom query failed");
            return;
        }
        state.SetIterationTime(fixture.timer.elapsed());
        falsePositives = static_cast<uint64_t>(positives);
        benchmark::DoNotOptimize(falsePositives);
    }

    fixture.setCounters(state);
    setFprCounters(state, falsePositives, fixture.numKmers);
}

}  // namespace benchmark_common

#define BENCHMARK_CUSBF_CONFIG_SYMBOL(K, S, M, H) CuSBF_K##K##_S##S##_M##M##_H##H##_Config
#define BENCHMARK_CUSBF_FIXTURE_SYMBOL(K, S, M, H) \
    CuSBF_K##K##_S##S##_M##M##_H##H##_Fixture
#define BENCHMARK_SUPERBLOOM_CPU_FIXTURE_SYMBOL(K, S, M, H) \
    SuperBloomCpu_K##K##_S##S##_M##M##_H##H##_Fixture

#define BENCHMARK_DEFINE_CUSBF_CONFIG_AND_FIXTURE(K, S, M, H)                         \
    using BENCHMARK_CUSBF_CONFIG_SYMBOL(K, S, M, H) = cusbf::Config<K, S, M, H, 256>; \
    using BENCHMARK_CUSBF_FIXTURE_SYMBOL(K, S, M, H) =                                \
        benchmark_common::CuSbfConfigFixture<BENCHMARK_CUSBF_CONFIG_SYMBOL(K, S, M, H)>;

#define BENCHMARK_DEFINE_CUSBF_ALL(FixtureName)                    \
    BENCHMARK_DEFINE_F(FixtureName, Insert)(benchmark::State & state) { \
        benchmark_common::runCuSbfInsert(*this, state);            \
    }                                                                   \
    BENCHMARK_DEFINE_F(FixtureName, Query)(benchmark::State & state) {  \
        benchmark_common::runCuSbfQuery(*this, state);             \
    }                                                                   \
    BENCHMARK_DEFINE_F(FixtureName, FPR)(benchmark::State & state) {    \
        benchmark_common::runCuSbfFpr(*this, state);               \
    }

#define BENCHMARK_DEFINE_CUSBF_FPR_ONLY(FixtureName)            \
    BENCHMARK_DEFINE_F(FixtureName, FPR)(benchmark::State & state) { \
        benchmark_common::runCuSbfFpr(*this, state);            \
    }

#define BENCHMARK_DEFINE_SUPERBLOOM_CPU_ALL(FixtureName)                \
    BENCHMARK_DEFINE_F(FixtureName, Insert)(benchmark::State & state) { \
        benchmark_common::runSuperBloomCpuInsert(*this, state);         \
    }                                                                   \
    BENCHMARK_DEFINE_F(FixtureName, Query)(benchmark::State & state) {  \
        benchmark_common::runSuperBloomCpuQuery(*this, state);          \
    }                                                                   \
    BENCHMARK_DEFINE_F(FixtureName, FPR)(benchmark::State & state) {    \
        benchmark_common::runSuperBloomCpuFpr(*this, state);            \
    }

#define BENCHMARK_DEFINE_SUPERBLOOM_CPU_FPR_ONLY(FixtureName)        \
    BENCHMARK_DEFINE_F(FixtureName, FPR)(benchmark::State & state) { \
        benchmark_common::runSuperBloomCpuFpr(*this, state);         \
    }

#define BENCHMARK_REGISTER_CUSBF_ALL(FixtureName) \
    REGISTER_BENCHMARK(FixtureName, Insert);           \
    REGISTER_BENCHMARK(FixtureName, Query);            \
    REGISTER_BENCHMARK(FixtureName, FPR);

#define BENCHMARK_REGISTER_CUSBF_FPR_ONLY(FixtureName) REGISTER_BENCHMARK(FixtureName, FPR);

#define BENCHMARK_REGISTER_SUPERBLOOM_CPU_ALL(FixtureName) \
    REGISTER_BENCHMARK(FixtureName, Insert);               \
    REGISTER_BENCHMARK(FixtureName, Query);                \
    REGISTER_BENCHMARK(FixtureName, FPR);

#define BENCHMARK_REGISTER_SUPERBLOOM_CPU_FPR_ONLY(FixtureName) \
    REGISTER_BENCHMARK(FixtureName, FPR);

#define BENCHMARK_CONFIG                \
    ->RangeMultiplier(2)                \
        ->Range(1 << 16, 1ULL << 28)    \
        ->Unit(benchmark::kMillisecond) \
        ->UseManualTime()               \
        ->Iterations(10)                \
        ->Repetitions(5)                \
        ->ReportAggregatesOnly(true)

#define BENCHMARK_CONFIG_FPR_FASTX_SWEEP \
    ->RangeMultiplier(2)                 \
        ->Range(1ULL << 22, 1ULL << 32)  \
        ->Unit(benchmark::kMillisecond)  \
        ->UseManualTime()                \
        ->Iterations(1)                  \
        ->Repetitions(1)                 \
        ->ReportAggregatesOnly(true)

#define REGISTER_BENCHMARK(FixtureName, BenchName) \
    BENCHMARK_REGISTER_F(FixtureName, BenchName)   \
    BENCHMARK_CONFIG

#define REGISTER_BENCHMARK_FPR_FASTX_SWEEP(FixtureName, BenchName) \
    BENCHMARK_REGISTER_F(FixtureName, BenchName)                   \
    BENCHMARK_CONFIG_FPR_FASTX_SWEEP

#define STANDARD_BENCHMARK_MAIN()                                   \
    int main(int argc, char** argv) {                               \
        ::benchmark::Initialize(&argc, argv);                       \
        if (::benchmark::ReportUnrecognizedArguments(argc, argv)) { \
            return 1;                                               \
        }                                                           \
        ::benchmark::RunSpecifiedBenchmarks();                      \
        ::benchmark::Shutdown();                                    \
        fflush(stdout);                                             \
        std::_Exit(0);                                              \
    }
